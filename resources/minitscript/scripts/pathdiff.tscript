# initialize
on: initialize
end

# scan folder
function: scanPath($basePath, $path, &$totalSize)
	$files = filesystem.list($path)
	if ($files == null)
		console.printLine("scanFolder(): could not list folder: " + $path)
	end
	forEach($file in $files)
		if ($file == "." || $file == "..")
			continue
		end
		if (filesystem.isPath(filesystem.composeURI($path, $file)) == false)
			continue
		end
		console.printLine(filesystem.composeURI($path, $file)->substring($basePath->getSize() + 1))
		scanPath($basePath, filesystem.composeURI($path, $file), $totalSize)
	end
	forEach($file in $files)
		if ($file == "." || $file == "..")
			continue
		end
		if (filesystem.isPath(filesystem.composeURI($path, $file)) == true) 
			continue
		end
		$fileSize = filesystem.getFileSize($path, $file)
		$totalSize = $totalSize + $fileSize 
		console.printLine(filesystem.composeURI($path, $file)->substring($basePath->getSize() + 1) + " - " + $fileSize)
	end
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	console.printLine("------------------")
	console.printLine("Path diff script")
	console.printLine("------------------")
	if (application.getArguments()->getSize() != 2)
		console.printLine("No path given: Please pass a path to scan to pathdiff script")
		script.stop()
	end
	# TODO: $path = application.getArguments()[1]
	$path = application.getArguments()->get(1)
	$totalSize = 0
	console.printLine("Scanning: " + $path)
	console.printLine()
	scanPath($path, $path, $totalSize)
	console.printLine()
	console.printLine("Total size: " + $totalSize)
	script.stop()
end

# an error has occurred
on: error
	console.printLine("------------------")
	console.printLine("Path diff script")
	console.printLine("------------------")
	console.printLine()
	console.printLine("An error occurred")
	script.stop()
end
