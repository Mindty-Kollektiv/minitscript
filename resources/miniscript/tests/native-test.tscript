# initialize
on: initialize
	console.printLine("------------------------")
	console.printLine("native-test: Initialize")
	console.printLine("------------------------")
	console.printLine()
end

function: testSingleScript($script, $className)
	$commands = []
	$commands[] = "rm -rf Tsrc"
	$commands[] = "mkdir Tsrc"
	$commands[] = "./bin/miniscript/tools/miniscripttranspiler " + $script + " Tsrc/" + $className
	$commands[] = "./bin/miniscript/tools/miniscriptmain " + $script + " " + $className + " Tsrc/Main-main.cpp"
	$commands[] = "./bin/miniscript/tools/miniscriptmakefile Tsrc TMakefile"
	$commands[] = "make -f TMakefile mains"
	forEach($command in $commands)
		console.print("Executing: " + $command)
		$exitCode = -1
		$error = null
		$result = application.execute($command, $exitCode, $error)
		if ($exitCode == $$.Application::EXITCODE_SUCCESS)
			console.printLine(", SUCCESS")
		else
			console.printLine(", FAILURE, exiting")
			console.printLine("**********************************************************************")
			console.printLine($error)
			application.exit($$.Application::EXITCODE_FAILURE)
		end
	end
	# execute binary
	$command = "./bin/Main"
	console.print("Executing: " + $command)
	$exitCode = -1
	$error = null
	$nativeResult = application.execute($command, $exitCode, $error)
	if ($exitCode != $$.Application::EXITCODE_SUCCESS)
		console.printLine(", FAILURE, exiting")
		console.printLine("**********************************************************************")
		console.printLine($error)
		application.exit($$.Application::EXITCODE_FAILURE)
	end
	console.printLine(", SUCCESS")
	# execute minscript interpreteer
	$command = "./bin/miniscript/tools/miniscript " + $script
	console.print("Executing: " + $command)
	$exitCode = -1
	$error = null
	$interpreterResult = application.execute($command, $exitCode, $error)
	if ($exitCode != $$.Application::EXITCODE_SUCCESS)
		console.printLine(", FAILURE, exiting")
		console.printLine("**********************************************************************")
		console.printLine($error)
		application.exit($$.Application::EXITCODE_FAILURE)
	end
	if ($nativeResult != $interpreterResult)
		console.printLine(", native result != interpreter result")
		console.printLine("**********************************************************************")
		console.printLine("\t" + $nativeResult->replace("\n","\n\t"))
		console.printLine("**********************************************************************")
		console.printLine("\t" + $interpreterResult->replace("\n","\n\t"))
		console.printLine("**********************************************************************")
	end
end

# if no condition is met, nothing will be executed, lol :D
on: nothing
	console.printLine("---------------------")
	console.printLine("native-test: Nothing")
	console.printLine("---------------------")
	console.printLine()
	testSingleScript("resources/miniscript/tests/application-test.tscript", "ApplicationTest")
	testSingleScript("resources/miniscript/tests/emit-test.tscript", "EmitTest")
	script.stop()
end

# an error has occurred
on: error
	console.printLine("------------------")
	console.printLine("native-test: Error")
	console.printLine("------------------")
	console.printLine("An error occurred")
	script.stop()
end